$version 12.00

// This is the root component of the entire GUI application.
$rect <20,20,220,60>
$output false
class Application : Core::Root
{
  $rect <10,540,210,580>
  inherited property Bounds = <0,0,390,390>;

  $rect <220,490,420,530>
  inherited method UpdateLayout()
  {
    /* initialize the rect effects depending on the current position of the ball images and depending on the given size */
    RectEffectRed.Value1    = BallRed.Bounds;
    RectEffectRed.Value2    = rect( BallRed.Bounds.x1, aSize.y - BallRed.Bounds.h, BallRed.Bounds.x2, aSize.y );
    RectEffectYellow.Value1 = BallYellow.Bounds;
    RectEffectYellow.Value2 = rect( BallYellow.Bounds.x1, aSize.y - BallYellow.Bounds.h, BallYellow.Bounds.x2, aSize.y );
    RectEffectGreen.Value1  = BallGreen.Bounds;
    RectEffectGreen.Value2  = rect( BallGreen.Bounds.x1, aSize.y - BallGreen.Bounds.h, BallGreen.Bounds.x2, aSize.y );

    /* start all rect effects */
    RectEffectRed.Enabled    = true;
    RectEffectYellow.Enabled = true;
    RectEffectGreen.Enabled  = true;

    /* set the screen size text */
    SizeText.String = string( Bounds.w ) + "x" + string( Bounds.h );

  }

  $rect <220,410,420,450>
  inherited method Init()
  {
    /* add a round mask for composer to get an impression of a round display... */
    $if $prototyper

    var Views::Image mask = new Views::Image;
    mask.Bitmap = Application::RoundMask;
    mask.Color = #000000C0;
    mask.Bounds = Bounds.orect;
    Add( mask, 0 );

    $endif
  }

  // This is the application class prepared for landscape and portrait mode.
  // 
  // It contains three RectEffects to animate the three ball images. The start- and \
  // end-coordinates of the animation are calculated within the method UpdateLayout().
  // 
  // A touch handler triggers the slot method 'OnTouch' which moves the cross image \
  // to the touched position and which prints the touch position as text.
  // This helps to verify, that your touch driver on the target provides correct \
  // coordinates.
  note legend Note1
  {
    attr Bounds = <430,0,1000,170>;
  }

  // This is a generic touch handler.
  $rect <20,20,160,60>
  object Core::SimpleTouchHandler SimpleTouchHandler
  {
    preset Point4 = <0,0>;
    preset Point3 = <390,0>;
    preset Point2 = <390,390>;
    preset Point1 = <0,390>;
    preset OnDrag = OnTouch;
    preset OnPress = OnTouch;
  }

  // This is a filled rectangle view.
  $rect <20,20,160,60>
  object Views::Rectangle Background
  {
    preset Bounds = <0,0,390,390>;
    preset ColorBL = #000000FF;
    preset ColorBR = #000000FF;
    preset ColorTR = #0000FFFF;
    preset ColorTL = #0000FFFF;
  }

  // This is an image view.
  $rect <20,20,160,60>
  object Views::Image BallRed
  {
    preset Bounds = <40,110,70,140>;
    preset Color = #FF483FFF;
    preset Bitmap = Application::Ball;
  }

  // This is an image view.
  $rect <20,20,160,60>
  object Views::Image BallYellow
  {
    preset Bounds = <80,190,110,220>;
    preset Color = #FFFB08FF;
    preset Bitmap = Application::Ball;
  }

  // This is an image view.
  $rect <20,20,160,60>
  object Views::Image BallGreen
  {
    preset Bounds = <310,160,340,190>;
    preset Color = #1DDA30FF;
    preset Bitmap = Application::Ball;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Caption
  {
    preset Bounds = <60,60,330,140>;
    preset WrapText = true;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "Screen Orientation Sample";
    preset Font = Application::FontLarge;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text TouchText
  {
    preset Bounds = <100,210,280,240>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "Touch Position";
    preset Font = Application::FontSmall;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Footer
  {
    preset Bounds = <60,280,330,310>;
    preset WrapText = true;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "www.embedded-wizard.de";
    preset Font = Application::FontSmall;
    preset Color = #FFFFFFFF;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Footer1
  {
    preset Bounds = <20,70,50,320>;
    preset Orientation = Views::Orientation.Rotated_270;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "Embedded Wizard";
    preset Font = Application::FontSmall;
    preset Color = #FFFFFFFF;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text Footer2
  {
    preset Bounds = <340,70,370,320>;
    preset Orientation = Views::Orientation.Rotated_90;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "Embedded Wizard";
    preset Font = Application::FontSmall;
    preset Color = #FFFFFFFF;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text PosText
  {
    preset Bounds = <80,230,310,270>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "X: -- Y: --";
    preset Font = Application::FontLarge;
  }

  // This is an image view.
  $rect <20,20,160,60>
  object Views::Image Cross
  {
    preset Bounds = <260,200,325,265>;
    preset Bitmap = Application::Cross;
  }

  // This is a move and resize rectangle effect.
  $rect <10,410,210,450>
  object Effects::RectEffect RectEffectRed
  {
    preset Symmetric = true;
    preset Timing = Effects::Timing.EaseIn_FastOut;
    preset CycleDuration = 1200;
    preset Outlet = ^BallRed.Bounds;
  }

  // This is a move and resize rectangle effect.
  $rect <10,450,210,490>
  object Effects::RectEffect RectEffectYellow
  {
    preset Symmetric = true;
    preset Timing = Effects::Timing.EaseIn_FastOut;
    preset CycleDuration = 1200;
    preset InitialDelay = 200;
    preset Outlet = ^BallYellow.Bounds;
  }

  // This is a move and resize rectangle effect.
  $rect <10,490,210,530>
  object Effects::RectEffect RectEffectGreen
  {
    preset Symmetric = true;
    preset Timing = Effects::Timing.EaseIn_FastOut;
    preset CycleDuration = 1200;
    preset InitialDelay = 500;
    preset Outlet = ^BallGreen.Bounds;
  }

  // This is a slot method connected with the touch handler. Each time the user touches \
  // on the screen, this method is called. As a result, the position of the cross \
  // image will be changed and the coordinates are shown as text.
  $rect <220,530,420,570>
  slot OnTouch
  {
    sender; /* the method is called from the sender object */

    /* modify the position of the image:
       get the touch position from the touch handler, adjust the position by half size of the image,
       in order to put the center of the image to the touched position */
    Cross.Bounds.origin = SimpleTouchHandler.CurrentPos - point ( Cross.Bounds.w / 2, Cross.Bounds.h / 2 );

    /* print the current touch position */
    PosText.String = "X: " + string( SimpleTouchHandler.CurrentPos.x ) + " Y: " + string( SimpleTouchHandler.CurrentPos.y );



  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text ScreenText
  {
    preset Bounds = <110,140,280,170>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset String = "Screen Size";
    preset Font = Application::FontSmall;
  }

  // This is an ordinary text view.
  $rect <20,20,160,60>
  object Views::Text SizeText
  {
    preset Bounds = <123,160,258,200>;
    preset Alignment = Views::TextAlignment[AlignHorzCenter, AlignVertCenter];
    preset Font = Application::FontLarge;
  }

  $rect <430,490,630,530>
  object Core::KeyPressHandler KeyHandler
  {
    preset OnPress = OnKeyPressed;
    preset Filter = Core::KeyCode.AnyKey;
    preset OnHold = OnKeyPressed;
  }

  $rect <430,530,630,570>
  slot OnKeyPressed
  {
    sender; /* the method is called from the sender object */

    var point pos = Cross.Bounds.center;

    /* move image just one pixel into desired direction */
    switch ( KeyHandler.Code )
    {
      case Core::KeyCode.Left:  pos = pos - <1,0>;
      case Core::KeyCode.Right: pos = pos + <1,0>;
      case Core::KeyCode.Up:    pos = pos - <0,1>;
      case Core::KeyCode.Down:  pos = pos + <0,1>;
      case Core::KeyCode.Key4:  pos = pos - <1,0>;
      case Core::KeyCode.Key6:  pos = pos + <1,0>;
      case Core::KeyCode.Key8:  pos = pos - <0,1>;
      case Core::KeyCode.Key2:  pos = pos + <0,1>;
      default:
        ;
    }

    /* limit position of cross */
    if ( pos.x < 0 ) pos.x = 0;
    if ( pos.x >= Bounds.w ) pos.x = Bounds.w - 1;
    if ( pos.y < 0 ) pos.y = 0;
    if ( pos.y >= Bounds.h ) pos.y = Bounds.h - 1;

    /* modify the position of the image */
    Cross.Bounds.origin = pos - point ( Cross.Bounds.w / 2, Cross.Bounds.h / 2 );

    /* print the current cross position */
    PosText.String = "X: " + string( pos.x ) + " Y: " + string( pos.y );


  }
}

// This is a bitmap resource.
$rect <440,60,640,100>
$output false
resource Resources::Bitmap Cross
{
  attr bitmapfile FileName = .\Res\Cross.png;
  attr framesize FrameSize;
  attr framedelay FrameDelay;
  attr bitmapformat Format = Alpha8;
  attr bitmapdithering Dithering = Auto;
  attr bitmapmode Mode = Default;
}

// This is a bitmap resource.
$rect <440,20,640,60>
$output false
resource Resources::Bitmap Ball
{
  attr bitmapfile FileName = .\Res\Ball.png;
  attr framesize FrameSize;
  attr framedelay FrameDelay;
  attr bitmapformat Format = Alpha8;
  attr bitmapdithering Dithering = Auto;
  attr bitmapmode Mode = Default;
}

// Font resource for large font - used for the caption text items.
$rect <230,20,430,60>
$output false
resource Resources::Font FontLarge
{
  attr fontname FontName = Arial;
  attr fontheight Height = 32;
  attr fontquality Quality = High;
  attr fontranges Ranges = 0x20-0xFF;
  attr fontaspectratio AspectRatio = 1.0;
  attr fontbold Bold = false;
  attr fontitalic Italic = false;
  attr fontkerning Kerning = true;
  attr fontheightmode HeightMode = Compatible;
  attr fontrowdistance RowDistance;
}

// Font resource for small font - used for the description text items.
$rect <230,60,430,100>
$output false
resource Resources::Font FontSmall
{
  attr fontname FontName = Arial;
  attr fontheight Height = 22;
  attr fontquality Quality = High;
  attr fontranges Ranges = 0x20-0xFF;
  attr fontaspectratio AspectRatio = 1.0;
  attr fontbold Bold = false;
  attr fontitalic Italic = false;
  attr fontkerning Kerning = true;
  attr fontheightmode HeightMode = Compatible;
  attr fontrowdistance RowDistance;
}

// This is the unit 'Application' of the 'ScreenOrientation' example.
// 
// The unit contains two different application classes: One for the landscape mode \
// (width > height) and one for the portrait mode (height > width).
// Each of them implements the appearance and the behavior of the simple UI application.
// 
// Additionally, the unit contains the necessary resources for the sample: Two different \
// font resources and two bitmap resources.
// The font resources are used to display text views, bitmap resources are used within \
// image views.
// 
// You can double-click on one of the application bricks to open the corresponding \
// class within the Composer.
note legend Note1
{
  attr Bounds = <10,130,670,360>;
}

$rect <640,20,840,60>
$output false
resource Resources::Bitmap RoundMask
{
  attr bitmapfile FileName = .\Res\390x390Mask.png;
  attr framesize FrameSize;
  attr framedelay FrameDelay;
  attr bitmapformat Format = Alpha8;
  attr bitmapdithering Dithering = Auto;
  attr bitmapmode Mode = Default;
}
